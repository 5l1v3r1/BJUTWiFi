apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.trevjonez.AndroidGithubReleasePlugin'

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    signingConfigs {
        release
    }
    defaultConfig {
        applicationId "me.liuyun.bjutlgn"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 5
        versionName "5.0"
        resConfigs "en", "zh-rCN"
    }
    buildTypes {
        debug {
            versionNameSuffix = " (${gitCommitCount})"
            signingConfig signingConfigs.release
        }
        release {
            versionNameSuffix = " (${gitCommitCount})"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "com.android.support:design:$support_lib_version"
    compile "com.android.support:cardview-v7:$support_lib_version"
    compile "com.android.support:support-dynamic-animation:$support_lib_version"
    compile 'io.reactivex.rxjava2:rxjava:2.0.6'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.github.HotBitmapGG:RingProgressBar:V1.2.2'
    compile 'com.j256.ormlite:ormlite-android:5.0'
    compile 'com.artit-k:license-fragment-support-v4:1.3.0'
    kapt "com.android.databinding:compiler:2.3.0"
}
kapt {
    generateStubs = true
}
AndroidGithubRelease {
    defaultConfig {
        tagName android.defaultConfig.versionName + "-${gitCommitCount}"
        owner "liu-yun"
        repo "BJUTWiFi"
        assets "build/outputs/apk"
        overwrite true
    }

    androidConfigs {
        debug {
            preRelease true
            tagModifier { it + "-debug" }
        }
        release {
            draft true
            releaseName android.defaultConfig.versionName
        }
    }
}

def propFile = file('../signing.properties')
if (propFile.canRead()) {
    Properties p = new Properties()
    p.load(new FileInputStream(propFile))

    if (p != null
            && p.containsKey('STORE_FILE')
            && p.containsKey('STORE_PASSWORD')
            && p.containsKey('KEY_ALIAS')
            && p.containsKey('KEY_PASSWORD')
            && p.containsKey('GITHUB_API_TOKEN')
    ) {
        println "Signing..."

        android.signingConfigs.release.storeFile = file(p['STORE_FILE'])
        android.signingConfigs.release.storePassword = p['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = p['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = p['KEY_PASSWORD']
        AndroidGithubRelease.defaultConfig.accessToken = p['GITHUB_API_TOKEN']

    } else {
        println "Required properties in signing.properties are missing"
        android.buildTypes.release.signingConfig = null
    }

} else {
    println "signing.properties not found"
    android.buildTypes.release.signingConfig = null
}